@init

function round(num)
(
  floor(num + 0.5);
);


function randInt(num)
(
  floor(rand(num) + 0.5);
);

// wrap midirecv and interpret values
// usage: while(msg.recv()) (msg.send())
function recv()
(
  //capture return value of midirecv and scope its args
  got = midirecv(m0, m1, m2, m3);
  got ? (
    this.offset = m0;
    // m1 = status byte, some 8 bit number;
    // shift away lowest 4 bits: 1111???? -> 1111
    this.statusHi = m1 >> 4;
    // mask top 4 bits: ????1010 & 00001111 -> 00001010
    this.statusLo = m1 & 15;
    this.data1 = m2;
    this.data2 = m3;
  );
  // return 1 or 0 for while() evaluation;
  got;

);

function send()
(
  midisend(
    this.offset,
    // reconstruct status by shifting high bits to the left
    // 1111 -> 11110000
    ((this.statusHi << 4) + this.statusLo),
    this.data1,
    this.data2
  );
);

function isNoteOn()
(
  this.statusHi == 9;
);

function isNoteOff()
(
  this.statusHi == 8;
);

function note()
(
  this.data1;
);
