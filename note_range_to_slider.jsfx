desc: Map Note Range to Slider + Note (GianniMIDI)
in_pin:none
out_pin:none

slider1:lowNote=36<0,128,1>first input note
slider2:inputChannel=1<1,16,1>input channel
slider3:iterations=16<1,64,1>iterations
slider4:output=0<0,1,0.00001>output
slider5:outputNote=36<0,127,1>output note
slider6:outputChannel=1<1,16,1>output channel

import my-lib.jsfx-inc


@init

function calcOutput()
(
  (this.data1 - lowNote) / iterations;
);


@block

while(msg.recv())
(
  msg.isNoteOn() && 
  msg.statusLo == (inputChannel - 1) &&
  isWithin(lowNote, (lowNote + iterations - 1), msg.data1) &&
  (
    output = msg.calcOutput();
    msg.data1 = outputNote;
    msg.statusLo = outputChannel - 1;
  );
  msg.isNoteOff() &&
  msg.statusLo == (inputChannel - 1) &&
  isWithin(lowNote, (lowNote + iterations - 1), msg.data1) &&
  (
    msg.sanitizeNoteOffs();
    msg.data1 = outputNote;
    msg.statusLo = outputChannel - 1;
  );
  msg.send();
);
