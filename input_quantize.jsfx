desc: gianniMIDI live input quantize

in_pin:none
out_pin:none

slider1:16<1,16,1>quantize interval (bar/n)
slider2:0<0,2,1{no (good for one-shots),unquantised (short notes may never play),smart quantized (min duration of 1 interval}>send note offs 

import gianni-lib.jsfx-inc

@init

queuedNoteOns = 0;
queuedNoteOnsLen = 0;
period = 0;
sCount = 0;

function enq()
(
  bits.value = 0;
  bits.writeBits(4, 0, this.statusLo);
  bits.writeBits(4, 4, this.statusHi);
  bits.writeBits(7, 8, this.data1);
  bits.writeBits(7, 15, this.data2);
  queuedNoteOns[queuedNoteOnsLen] = bits.value;
  queuedNoteOnsLen += 1;
);

function deq(index)
(
  bits.value = queuedNoteOns[index];
  midisend(
    0,
    bits.readBits(8, 0),
    bits.readBits(7, 8),
    bits.readBits(15, 8)
  );
  queuedNoteOns[index] = 0;
);

function releaseQueuedNoteOns()
(
  while(queuedNoteOnsLen > 0)
  (
    queuedNoteOnsLen -= 1;
    deq(queuedNoteOnsLen);
  );
);

@slider

period = samplesPerBar() / slider1;

@block

!period && period = samplesPerBar() / slider1;

@sample

while (msg.recv())
(
  handled = 0;
  msg.isNoteOn() &&
  (
    handled = 1;
    msg.enq();
  );
  msg.isNoteOff() &&
  (
    handled = 1;
    slider2 == 1 && msg.send()
  ) ;
  !handled && msg.send();
);
sCount += 1;
sCount >= period && sCount = 0;
sCount == 0 && releaseQueuedNoteOns();
