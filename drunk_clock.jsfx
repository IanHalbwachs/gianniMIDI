desc: gianniMIDI drunk clock 

in_pin:none
out_pin:none

slider1:8<1,16,1>sober period (1/n bars)
slider2:2<1,8,1>drunk period (sober period /n)
slider3:0<0,4,1>offset n drunk periods
slider4:0<-1,1,.01>drunk factor
slider5:60<0,127,1>output note


import gianni-lib.jsfx-inc


@init

on.statusHi = 9;
on.data1 = 60;
on.data2 = 100;

off.statusHi = 8;
off.data1 = 60;

drunkClock.started = 0;

function setPeriods()
(
  soberClock.setPeriodBars(1 / slider1);
  drunkClock.setPeriod(soberClock.period / (slider2 - slider4));
  soberClock.offset = slider3 * soberClock.period / slider2;
);


@slider

setPeriods();
on.data1 = slider5;
off.data1 = on.data1;


@block

sampleOffset = -1;


@sample

sampleOffset += 1;

tempoChanged() && setPeriods();

soberClock.tick() &&
(
  drunkClock.started = 1;
  drunkClock.cInit();
);

drunkClock.started && drunkClock.tick() &&
(
  off.sendWithOffset(sampleOffset);
  on.sendWithOffset(sampleOffset);
);
